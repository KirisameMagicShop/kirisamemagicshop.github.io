+++
title = "愿你走出半生，归来仍是Java Parser"
author = "圆角骑士魔理沙"
date = 2018-12-15T05:17:00+08:00
tags = ["函数式编程", "Haskell", "面向对象编程"]
license = "CC BY-SA 4.0"
+++
几天前，我的一个朋友给了我一个Haskell问题

 > 
 > Hey, MK，假设我有个BNF，并且我在Haskell中有个这个BNF的parser。  
 > 现在，我想给这个BNF改一行，有没有办法不用动这个BNF parser的代码（因为是其他人写的），而是对这parser进行扩展呢？

这问题挺有趣的，也不算难。

这问题说是extensibility problem，其实有两个地方需要扩展。

0：Parser需要用open recursion之类的方法扩展

1：Parse出来的ADT也需要可扩展性

后半个需求见多了，Final Tagless，DTALC，Tree that grow，Recursion scheme style fix。。。于是放下不表，我们来处理前一个。

前半个。。Haskell's Overlooked Object System就搞过，当然他们有点heavy weight，打算随手弄一个**超级轻量级的**：5行就够了，多一行是小莎莎。

Ready？

````haskell
data Object x = MkObject (x -> x)
````

1。Inheritance is not subtyping式的Object=recursive type。为了简易性（反正也不需要多高的扩展性）就不model真。recursive type，而只有recursive dependency。

````haskell
use :: Object x -> x
use (MkObject x) = let res = x res in res
````

2。3。最典型的tying the knot。其实就是fix了。

我们想想，这个x是什么variant的呢？covariant还是contravariant？

````haskell
inherit :: (a -> b) -> (b -> a) -> Object a -> Object b
inherit ab ba (MkObject aa) = MkObject (ab . aa . ba)
````

既然是invariant，那fmap contramap都用不上，但invariant依然能有map：两边一起传进来就行了。4。5。

这就是一个prototype based oo system了。

接下来讲怎么用哈：

````haskell
test :: Object (Int, Int)
test = MkObject $ \self -> (2, fst self + fst self)
````

这弄了个两个field的object，第零个field初始值为2（可能因为继承被override），第一个field为第零个field的值\*2（不一定是3，如果任何field被override这个值都能改）。use test应该是(2, 4)。

````haskell
inheritTest :: Object ((Int, Int), Int)
inheritTest = inherit (\(l, r) -> ((l + 1, r + 2), r + 1)) fst test
````

这里继承了上面的Object，override了l(l + 1是super + 1），r被override到super + 2，加了个新的field，值是r+1。use inheritTest应该是((3, 8), 7)。记着传进来的参数不是self而是super就很好理解了。

好，open recursion搞好了，剩下的就是标准的final tagless了，体力活，没啥意思

````haskell
class AST repr where
  lit :: Int -> repr
  plus :: repr -> repr -> repr

class Var repr where
  var :: String -> repr

type WholeParser repr = Parser repr
type LitParser repr = Parser repr
type PlusParser repr = Parser repr

intP :: Parser Int
intP = read <$> many1 digit

stringP :: Parser String
stringP = many1 letter

type OriginalParser repr = ((LitParser repr, PlusParser repr), WholeParser repr)
originalParser :: AST repr => Object (OriginalParser repr)
originalParser = MkObject $ \(~(_, p)) -> let
  litP = lit <$> intP
  plusP = between (char '(') (char ')') (do {l <- p; spaces; char '+'; spaces; r <- p; return $ plus l r})
  wholeP = litP <|> plusP in
  ((litP, plusP), wholeP)

type VarParser repr = Parser repr
extendedParser :: (AST repr, Var repr) => Object (VarParser repr, OriginalParser repr)
extendedParser = inherit extend snd originalParser
  where
    extend ~((litP, plusP), wholeP) = let
      varP = var <$> stringP in
      (varP, ((litP, plusP), varP <|> wholeP))

instance AST String where
  lit = show
  plus x y = "(" ++ x ++ " " ++ "+" ++ " " ++ y ++ ")"

instance Var String where
  var x = x
````

大功告成。

代码在[https://github.com/MarisaKirisame/extensible-parser/blob/master/src/Lib.hs](https://github.com/MarisaKirisame/extensible-parser/blob/master/src/Lib.hs)

  


Q：封装呢？

A：Abstract Type is Existential Type

  


Q：这是prototype based的，class怎么办？

A：A Theory Of Object里面讲过怎么用prototype来做class

  


Q：多继承呢？

A：给定Object a，Object b，可以组合出Object (a, b)，要菱形继承自己手动再inherit一下就好

  


Q：Subtyping?

A：Typeclass。

  


如果大家感兴趣，请评论下，我可以再写个blog把这些功能补完。
