+++
title = "2020 Dyalog APL Comp 第一部分解"
author = "LdBeth"
date = 2020-10-24T09:16:00+08:00
tags = ["APL", "函数式编程", "Code Golf", "雾雨魔法店"]
license = "转自[知乎](https://zhuanlan.zhihu.com/p/268165587)"
+++
因为我太菜了，虽然其实第一部分都做出来了，但也就想好第一题比较理想的 tacit 解

![](v2-91358355e32b3871ed5b84fee560de76_b.jpg)
````apl
{(⌽⍣(⍺<0))(⍺↑⍵)(⍺↓⍵)}
````

显然的解。

APLcart 给了个 tacit idiom, 注意用到了 18.0 新出的 Over operator，在 17.1 只能用 $\{\alpha\; \omega\}$ 

````apl
Is(↑,⍥⊂↓)Y ⍝ ←→ Is (↑{⍺ ⍵}↓) Y ←→ Is {(⍺↑⍵)(⍺↓⍵)} Y
````

但不能直接用上去，因为在 $\alpha$ 是负数的时，结果和要求是反向的

````apl
      ¯3 (↑,⍥⊂↓) 'FooBar'
┌───┬───┐
│Bar│Foo│
└───┴───┘
````

这就是为啥我用了 reverse，并用 power operator 控制，对应 $\alpha <0, \alpha >0$ 两个 case。但用了 power operator 就很难转成 tacit form 了。那么上面的答案要如何写成 tacit 呢。

可以換个角度，在负数时转成正数。假设有 f 做了这个工作，那么

````apl
{(a↑⍵)((a←⍺ f ⍵)↓⍵)} ⍝ ←→ {(⍺ f ⍵)(↑,⍥⊂↓)⍵} ←→ f(↑,⍥⊂↓)⊢
````

那么 f 是什么呢，是这个么？

````apl
{⍺|⍨≢⍵}
````

殘念，并不是，反例：

````apl
      6 {⍺|⍨≢⍵} 'FooBar'
0
````

那就加个 a = #w 的 special case 好了

````apl
      812 {⍺⌈⍺|⍨⍵} 812
812
      ¯812 {⍺⌈⍺|⍨⍵} 812
0
⍝ f ←→  (⊣⌈⊣|⍨≢∘⊢) ←→ (⊣⌈|⍨∘≢)
````

Final answer

````apl
      ((⊣⌈|⍨∘≢)(↑,⍥⊂↓)⊢) ⍝ ((⊣⌈|⍨∘≢)(↑{⍺ ⍵}↓)⊢) for Dyalog 17.1
````

![](v2-4367935fdf559969f7104fe94391321a_b.jpg "对比下三种写法的長度")---

附赠第七题解

![](v2-a2126b822ee53a53ff1964667e2353d6_b.jpg)
````apl
(∧/⊣∊⍥(⍸∘⌽∘(2∘⊥⍣¯1))⊢) ⍝{f←⍸∘⌽∘(2∘⊥⍣¯1) ⋄ ∧/(f ⍺)∊f ⍵} for 17.1
````

![](v2-2651edd52b36ce263730a75155ac8793_b.jpg "nothing particular, 就是考 decode 的应用而已")---

干脆都放上来了

![](v2-13e3840919b3e283a128e350531b882a_b.jpg "partition 的应用")
````apl
(⊢⊂⍨128∘>∨191∘<)
````

![](v2-a16cb494adeb87f2ddde8cf28287d6dd_b.jpg "还是 decode encode")![](v2-b5c0564865e1c2d35baf369f5958a80a_b.jpg "trivial 到炸了")![](v2-7734970e46b77c015bc859f8006011f2_b.jpg "well，我从 APLcart 拿了 arithmetic progression 改了下")![](v2-6a07561817677a2ba786db8c622afed8_b.jpg "mesh 的应用")![](v2-29ddff47164e05c25cde152136f4e47c_b.jpg "写的有点糟")
````apl
{∧/¯1=2×/×2-/(10∘⊥⍣¯1)⍵}
````

![](v2-f0ffa2762beb120565d5988d8146d15c_b.jpg "同样，写的比较槽")
````apl
{⍵≡(⌽⌈\⌽⍵)⌊⌈\⍵}
````

![](v2-b1834c4c84a672793f39b03bac5faf4a_b.jpg "不难，但是感覺没啥实际用处")
